buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:2.1.0'
  }
}

apply plugin: 'com.android.application'

ext.rootAppId = "com.github.charmasaur.alpsinsects"

def getContentProviderAuthority(suffix) {
  return rootAppId + ".provider" + suffix + ".FieldGuideContentProvider"
}

def getAssetsProviderAuthority(suffix) {
  return rootAppId + ".provider" + suffix + ".FieldGuideAssetsProvider"
}

def getSearchSuggestIntentData(suffix) {
  return "content://" + getContentProviderAuthority(suffix) + "/fieldguide"
}

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.3"
  lintOptions {
    abortOnError true
  }
  defaultConfig {
    applicationId "${rootAppId}"
    minSdkVersion 16
    targetSdkVersion 23
    versionCode 10
    versionName "1.0"
    vectorDrawables.useSupportLibrary = true
  }
  buildTypes {
    debug {
      def final suffix = ".debug"
      resValue "string", "app_name", "Alps Insects Debug"

      applicationIdSuffix "${suffix}"
      versionNameSuffix "${suffix}"

      buildConfigField "String", "CONTENT_PROVIDER_AUTHORITY", "\"${getContentProviderAuthority(suffix)}\""
      buildConfigField "String", "ASSETS_PROVIDER_AUTHORITY", "\"${getAssetsProviderAuthority(suffix)}\""

      resValue "string", "content_provider_authority", "${getContentProviderAuthority(suffix)}"
      resValue "string", "assets_provider_authority", "${getAssetsProviderAuthority(suffix)}"
      resValue "string", "suggest_intent_data", "${getSearchSuggestIntentData(suffix)}"
    }
    release {
      def final suffix = ""
      resValue "string", "app_name", "Alps Insects"

      applicationIdSuffix "${suffix}"
      versionNameSuffix "${suffix}"

      buildConfigField "String", "CONTENT_PROVIDER_AUTHORITY", "\"${getContentProviderAuthority(suffix)}\""
      buildConfigField "String", "ASSETS_PROVIDER_AUTHORITY", "\"${getAssetsProviderAuthority(suffix)}\""

      resValue "string", "content_provider_authority", "${getContentProviderAuthority(suffix)}"
      resValue "string", "assets_provider_authority", "${getAssetsProviderAuthority(suffix)}"
      resValue "string", "suggest_intent_data", "${getSearchSuggestIntentData(suffix)}"
    }
  }
}

repositories {
  mavenCentral()
}

dependencies {
  compile 'com.android.support:appcompat-v7:23.3.0'
  compile 'com.android.support:support-v4:23.3.0'
  compile 'com.google.code.gson:gson:2.6.2'
  compile 'com.github.bigfishcat.android:svg-android:2.0.7'
  compile 'it.sephiroth.android.library.imagezoom:imagezoom:2.2.5'
}
